#! /usr/bin/env zsh

#
# ::: Utilities :::
#

function ftime; printf "%02d:%02d:%06.3f" $(( $1 / 3600 )) $(( ($1 / 60) % 60 )) $(( $1 % 60 ))

#
# ::: Library :::
#

function convert_chunks_and_add_delay_to_them {
  local -a files_list=($@)

  foreach f ($files_list) {
    ffmpeg -y \
           -v quiet \
           -i $f \
           -vn \
           -af adelay=1s:all=true \
           -acodec libopus \
            tmp/${f:t:r}.opus
  }
}

function create_metadata_file_with_chapters {
  local chapter_title=$1
  local chapter_start=0.0
  local chapter_duration=0.0
  local chapter_counter=1
  local chapter_start_tag
  local chapter_name_tag

  touch tmp/metadata.txt

  foreach f (tmp/*.opus) {
    chapter_duration=$(ffprobe -v quiet -i $f -show_entries stream=duration -of default=noprint_wrappers=1:nokey=1)

    chapter_start_tag="CHAPTER$(printf "%03d" $chapter_counter)=$(ftime $chapter_start)"
    chapter_name_tag="CHAPTER$(printf "%03d" $chapter_counter)NAME=$chapter_title $(printf "%d" $chapter_counter)"

    printf "%s\n%s\n" $chapter_start_tag $chapter_name_tag >> tmp/metadata.txt

    chapter_start=$(($chapter_start + $chapter_duration))

    chapter_counter=$(($chapter_counter + 1))
  }
}

function concatenate_chunks {
  ffmpeg -y \
         -v quiet \
         -f concat \
         -safe 0 \
         -i <(foreach f (tmp/*.opus) { printf "file '%s/%s'\n" $(pwd) $f }) \
         -af apad=pad_dur=1s \
         -acodec libopus \
         -map_metadata -1 \
          tmp/raw_output.opus
}

function add_chapters {
  local output=$1
  local -a options

  options+=(-v quiet -i tmp/raw_output.opus)

  foreach line ("${(f)$(<tmp/metadata.txt)}") { options+=(-metadata $line) }

  ffmpeg ${(@)options} $output
}

#
# ::: Main :::
#

function main {
  local -a args=($@)

  local -a options
  local -a input
  local    title
  local    output

  options=(${args:0:-1})

  input=(${~options[options[(i)-i|--input] + 1]:-${${options[(r)--input=*]#--input=}:-undefined}})
  title=${options[options[(i)-t|--title] + 1]:-${${options[(r)--title=*]#--title=}:-undefined}}
  output=${args[-1]}

  [[ $input =~ undefined ]] || \
  [[ $title =~ undefined ]] && \
    echo "Both input and title should be specified.\n Input: $input\n Title: $title" && \
    bye

  mkdir tmp

  convert_chunks_and_add_delay_to_them $input
  create_metadata_file_with_chapters $title
  concatenate_chunks
  add_chapters $output

  rm -rf tmp
}

main $@

