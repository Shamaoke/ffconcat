#! /usr/bin/env zsh

#
# ::: Utilities :::
#

function convert_time {
  local h=$(( $1 / 3600 ))
  local m=$(( ($1 / 60) %60 ))
  local s=$(( $1 % 60 ))

  printf "%02d:%02d:%06.3f" $h $m $s
}

#
# ::: Library :::
#

function convert_chunks_and_add_delay_to_them {
  foreach f (assets/caps/16ed/*.webm) {
    ffmpeg -y \
           -v quiet \
           -i $f \
           -vn \
           -af adelay=1s:all=true \
           -acodec libopus \
            tmp/${f:t:r}.opus
  }
}

function create_metadata_file_with_chapters {
  local chapter_start=0.0
  local chapter_duration=0.0
  local chapter_counter=1
  local chapter_start_tag
  local chapter_name_tag

  touch tmp/metadata.txt

  foreach f (tmp/*.opus) {
    chapter_duration=$(ffprobe -v quiet -i $f -show_entries stream=duration -of default=noprint_wrappers=1:nokey=1)

    chapter_start_tag="CHAPTER$(printf "%03d" $chapter_counter)=$(convert_time $chapter_start)"
    chapter_name_tag="CHAPTER$(printf "%03d" $chapter_counter)NAME=$(printf "%d" $chapter_counter)"

    printf "%s\n%s\n" $chapter_start_tag $chapter_name_tag >> tmp/metadata.txt

    chapter_start=$(($chapter_start + $chapter_duration))

    chapter_counter=$(($chapter_counter + 1))
  }
}

function concatenate_cunks {
  ffmpeg -y \
         -v quiet \
         -f concat \
         -safe 0 \
         -i <(foreach f (tmp/*.opus) { printf "file '%s/%s'\n" $(pwd) $f }) \
         -af apad=pad_dur=1s \
         -acodec libopus \
          tmp/raw_output.opus
}

function add_metadata {
  local -a cmd=(ffmpeg -v quiet -i tmp/raw_output.opus)

  cat tmp/metadata.txt | while read line
  do
    cmd+=(-metadata $line)
  done

  cmd+=(output.opus)

  $cmd[@]
}

#
# ::: Main :::
#

function main {

  mkdir tmp

  convert_chunks_and_add_delay_to_them
  create_metadata_file_with_chapters
  concatenate_cunks
  add_metadata

  rm -rf tmp

}

main

